	PUBLIC	MINIT,MON,MKEY,MOFF,MSAV,MRST
	EXTRN	SCR_BUF:BYTE,S_B_PTR:WORD
	EXTRN	SV_V_MOD:BYTE,V_MON_TYP:BYTE,V_AD_TYP:BYTE,VB_FRAME:WORD
	EXTRN	ST_CUR_LIN:BYTE,EN_CUR_LIN:BYTE
	EXTRN	ATTR_ADR:WORD,C_ATTR:BYTE,M_ATTR:BYTE
DGROUP	GROUP	ASMEXT_DATA
ASMEXT_TEXT	SEGMENT	PARA	PUBLIC	'CODE'
	ASSUME	CS:ASMEXT_TEXT,DS:DGROUP,ES:DGROUP,SS:NOTHING
MINIT	PROC	FAR
	PUSH	BP
	PUSH	SI
	PUSH	DI
	LEA	AX,[SCR_BUF]
	MOV	[S_B_PTR],AX
	MOV	AX,1130H
	XOR	BX,BX
	XOR	CX,CX
	XOR	DX,DX
	INT	10H
	XOR	AX,AX
	MOV	AL,DL
	MUL	CX
	CMP	AX,200
	JB	NOEGA
	PUSH	CX
	MOV	AH,12H
	MOV	BX,10H
	INT	10H
	POP	CX
	MOV	BL,BH
	XOR	BH,BH
	MOV	DX,1
	JMP	SHORT	L1
NOEGA:	MOV	AX,40H
	MOV	ES,AX
	XOR	BX,BX
	XOR	DX,DX
	MOV	AL,ES:[BX+10H]
	AND	AL,00110000B
	CMP	AL,00110000B
	JNE	L1
	INC	BX
L1:	PUSH	BX
	PUSH	CX
	PUSH	DX
	MOV	AH,0FH
	INT	10H
	POP	DX
	POP	CX
	POP	BX
	MOV	[SV_V_MOD],AL
	MOV	[V_MON_TYP],BL
	MOV	[V_AD_TYP],DL
	MOV	AX,3
	MOV	[ATTR_ADR],OFFSET C_ATTR
	MOV	[VB_FRAME],0B800H
	MOV	[ST_CUR_LIN],7
	MOV	[EN_CUR_LIN],7
	TEST	BX,BX
	JZ	L3
	MOV	AX,7
	MOV	[ATTR_ADR],OFFSET M_ATTR
	MOV	[VB_FRAME],0B000H
	MOV	[ST_CUR_LIN],11
	MOV	[EN_CUR_LIN],12
L3:	PUSH	BX
	PUSH	CX
	PUSH	DX
	INT	10H
	POP	DX
	POP	CX
	POP	BX
	TEST	DX,DX
	JNZ	L4
	MOV	DX,3D8H
	TEST	BX,BX
	JZ	L5
	MOV	DX,3B8H
L5:	MOV	AL,00001001B
	OUT	DX,AL
	JMP	SHORT	L6
L4:	SUB	CX,3
	MOV	[ST_CUR_LIN],CL
	INC	CX
	MOV	[EN_CUR_LIN],CL
	MOV	AX,1003H
	XOR	BX,BX
	INT	10H
L6:	MOV	[MPR],0
	MOV	AX,3533H
	INT	21H
	MOV	AX,ES
	TEST	AX,AX
	JZ	NOM
	XOR	AX,AX
	INT	33H
	TEST	AX,AX
	JZ	NOM
	MOV	AX,0AH
	XOR	BX,BX
	MOV	CX,1111111111111111B
	MOV	DX,0111011100000000B
	INT	33H
	MOV	[MPR],0FFH
	MOV	[MFBPTR],OFFSET MFB
NOM:	POP	DI
	POP	SI
	POP	BP
	RET
MINIT	ENDP
MON	PROC	FAR
	CMP	[MPR],0
	JZ	NM1
	CMP	[MCF],0
	JNZ	NM1
	MOV	AX,1
	INT	33H
	MOV	[MCF],0FFH
NM1:	RET
MON	ENDP
MKEY	PROC	FAR
	PUSH	BP
	PUSH	SI
	PUSH	DI
	MOV	BP,SP
	MOV	AH,1
	INT	16H
	JNZ	KEYB
	XOR	AX,AX
	CMP	[MPR],0
	JZ	NM2
	MOV	AX,5
	XOR	BX,BX
	INT	33H
	TEST	BX,BX
	JNZ	MOUSE
	MOV	AX,5
	MOV	BX,1
	INT	33H
	TEST	BX,BX
	JNZ	MOUSE
NM2:	MOV	BX,[BP+10H]
	MOV	WORD PTR [BX],0
	MOV	BX,[BP+0AH]
	MOV	[BX],AX
	POP	DI
	POP	SI
	POP	BP
	RET	8
KEYB:	XOR	AX,AX
	INT	16H
	XOR	DX,DX
	MOV	BX,[BP+10H]
	MOV	WORD PTR [BX],1
	MOV	BX,[BP+0EH]
	MOV	DL,AL
	MOV	[BX],DX
	MOV	BX,[BP+0CH]
	MOV	DL,AH
	MOV	[BX],DX
	POP	DI
	POP	SI
	POP	BP
	RET	8
MOUSE:	MOV	SI,CX
	MOV	CX,3
	SHR	SI,CL
	SHR	DX,CL
	MOV	BX,[BP+10H]
	MOV	WORD PTR [BX],2
	MOV	BX,[BP+0EH]
	MOV	[BX],DX
	MOV	BX,[BP+0CH]
	MOV	[BX],SI
	MOV	BX,[BP+0AH]
	MOV	[BX],AX
	POP	DI
	POP	SI
	POP	BP
	RET	8
MKEY	ENDP
MOFF	PROC	FAR
	CMP	[MPR],0
	JZ	NM3
	CMP	[MCF],0
	JZ	NM3
	MOV	AX,2
	INT	33H
	MOV	[MCF],0
NM3:	RET
MOFF	ENDP
MSAV	PROC	FAR
	CMP	[MPR],0
	JZ	NM4
	MOV	BX,[MFBPTR]
	MOV	AL,[MCF]
	MOV	[BX],AL
	INC	BX
	MOV	[MFBPTR],BX
NM4:	RET
MSAV	ENDP
MRST	PROC	FAR
	CMP	[MPR],0
	JZ	NM5
	MOV	BX,[MFBPTR]
	DEC	BX
	MOV	AL,[BX]
	MOV	AH,[MCF]
	CMP	AL,AH
	JE	L7
	PUSH	AX
	PUSH	BX
	MOV	AL,1
	TEST	AH,AH
	JZ	L8
	INC	AL
L8:	XOR	AH,AH
	INT	33H
	POP	BX
	POP	AX
	MOV	[MCF],AL
L7:	MOV	[MFBPTR],BX
NM5:	RET
MRST	ENDP
ASMEXT_TEXT	ENDS
ASMEXT_DATA	SEGMENT	WORD	PUBLIC	'DATA'
MPR	DB	?
MCF	DB	0
MFB	DB	8 DUP(?)
MFBPTR	DW	?
ASMEXT_DATA	ENDS
	END
