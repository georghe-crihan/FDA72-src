PROWCNT	DW	?
PCOLCNT	DW	?
PBYTCNT	DW	?
PBITCNT	DW	?
PBYTPTR	DW	?
PBITPTR	DW	?
OURBYTE	DB	?
OURMASK	DB	?
ONCETBL	DB	1BH,40H,1BH,33H,24,4 DUP(0AH)
INITTBL	DB	1BH,'*',38,0BCH,2,180 DUP(0)
CRLFTBL	DB	0DH,0AH
EXITTBL	DB	1BH,40H
GRAPHSCRPR	PROC	FAR
	PUSH	BP
	PUSH	SI
	PUSH	DI
	MOV	AX,3524H
	INT	21H
	PUSH	BX
	PUSH	ES
	MOV	AX,2524H
	PUSH	DS
	MOV	DX,CS
	MOV	DS,DX
	LEA	DX,[HOOK24]
	INT	21H
	POP	DS
	MOV	AH,0FH
	INT	10H
	CMP	AL,12H
	JE	@F
	MOV	AX,0E07H
	MOV	BX,7
	INT	10H
	JMP	DONE
@@:	MOV	AH,40H
	MOV	BX,4
	MOV	CX,9
	LEA	DX,[ONCETBL]
	INT	21H
	JNC	@F
	JMP	DONE
@@:	MOV	[PROWCNT],0
ROWCYC:	MOV	AH,40H
	MOV	BX,4
	MOV	CX,185
	LEA	DX,[INITTBL]
	INT	21H
	JNC	@F
	JMP	DONE
@@:	MOV	[PCOLCNT],0
COLCYC:	MOV	[PBYTCNT],0
	MOV	AX,[PROWCNT]
	MOV	[PBYTPTR],AX
BYTCYC:	MOV	[PBITCNT],0
	MOV	AX,[PBYTPTR]
	MOV	[PBITPTR],AX
	MOV	[OURBYTE],0
	MOV	[OURMASK],0C0H
BITCYC:	MOV	AH,0DH
	XOR	BH,BH
	MOV	CX,[PCOLCNT]
	MOV	DX,[PBITPTR]
	INT	10H
	TEST	AL,AL
	JZ	@F
	MOV	AL,[OURMASK]
	OR	[OURBYTE],AL
@@:	SHR	[OURMASK],1
	SHR	[OURMASK],1
	INC	[PBITPTR]
	INC	[PBITCNT]
	CMP	[PBITCNT],4
	JB	BITCYC
	MOV	AH,40H
	MOV	BX,4
	MOV	CX,1
	LEA	DX,[OURBYTE]
	INT	21H
	JC	DONE
	ADD	[PBYTPTR],4
	INC	[PBYTCNT]
	CMP	[PBYTCNT],3
	JB	BYTCYC
	INC	[PCOLCNT]
	CMP	[PCOLCNT],640
	JB	COLCYC
	MOV	AH,40H
	MOV	BX,4
	MOV	CX,2
	LEA	DX,[CRLFTBL]
	INT	21H
	JC	DONE
	ADD	[PROWCNT],12
	CMP	[PROWCNT],480
	JNB	@F
	JMP	ROWCYC
@@:	MOV	AH,40H
	MOV	BX,4
	MOV	CX,2
	LEA	DX,[CRLFTBL]
	INT	21H
DONE:	MOV	CX,DS
	POP	DS
	POP	DX
	MOV	AX,2524H
	INT	21H
	MOV	DS,CX
	POP	DI
	POP	SI
	POP	BP
	RET
GRAPHSCRPR	ENDP
HOOK24:	STI
	PUSH	AX
	PUSH	BX
	PUSH	CX
	PUSH	DX
	PUSH	BP
	PUSH	SI
	PUSH	DI
	PUSH	DS
	PUSH	ES
	MOV	AX,0E07H
	MOV	BX,7
	INT	10H
	POP	ES
	POP	DS
	POP	DI
	POP	SI
	POP	BP
	POP	DX
	POP	CX
	POP	BX
	POP	AX
	MOV	AL,03H
	IRET
